plugins {
    // Processes Lombok annotations during build
    id 'io.franzbecker.gradle-lombok' version '3.1.0'

    // Creates fat JAR
    id 'com.github.johnrengelman.shadow' version '5.0.0'

    // Downloads binary dependencies
    id 'de.undercouch.download' version '3.4.3'

    // Adds dependencyUpdates task
    id 'com.github.ben-manes.versions' version '0.21.0'
}

def gradleDependencyVersion = '5.4.1'

wrapper {
    gradleVersion = gradleDependencyVersion
}

apply plugin: 'java'
apply plugin: 'application'
apply plugin: 'idea'

// Use the GGProvisioner main class when the JAR is invoked directly
mainClassName = 'com.awslabs.aws.greengrass.provisioner.AwsGreengrassProvisioner'

distZip.enabled = shadowDistZip.enabled = false
distTar.enabled = shadowDistTar.enabled = false

// Specify all of our dependency versions
def guiceVersion = '4.2.2'
def configVersion = '1.3.4'
def ztZipVersion = '1.13'
def jcommanderVersion = '1.72'
def slf4jVersion = '1.7.26'
def jcabiVersion = '0.18'
def jtarVersion = '2.3'
def gsonVersion = '2.8.5'
def dockerClientVersion = '8.16.0'
def awsSdk2Version = '2.5.52'
def mavenInvokerVersion = '3.0.1'
def commonsLangVersion = '3.9'
def junitVersion = '4.12'
def systemRulesVersion = '1.19.0'
def mockitoVersion = '2.28.2'
def lombokVersion = '1.18.8'
def vavrVersion = '0.10.0'
def httpClientVersion = '4.5.8'
def jodahFailsafeVersion = '2.0.1'
def testContainersVersion = '1.11.3'
def hamcrestVersion = '2.1'

def buildDirDist = "$buildDir/dist"
def buildDirTemp = "$buildDir/temp"
def buildDirFoundation = "$buildDir/foundation"

task downloadGreengrassBinaries(type: Download) {
    // Download the Greengrass binaries into the dist directory so they can be packaged into the JAR
    src(
            ['https://d1onfpft10uf5o.cloudfront.net/greengrass-core/downloads/1.9.1/greengrass-linux-aarch64-1.9.1.tar.gz',
             'https://d1onfpft10uf5o.cloudfront.net/greengrass-core/downloads/1.9.1/greengrass-linux-armv7l-1.9.1.tar.gz',
             'https://d1onfpft10uf5o.cloudfront.net/greengrass-core/downloads/1.9.1/greengrass-linux-x86-64-1.9.1.tar.gz']
    )
    dest "$buildDirDist"
    overwrite false
}

def nodeSdkTarGz = 'aws-greengrass-core-sdk-js-1.4.0.tar.gz'
def nodeSdkZip = 'aws-greengrass-core-sdk-js.zip'

task fetchGreengrassNodeSdk(type: Download) {
    src "https://d1onfpft10uf5o.cloudfront.net/greengrass-sdk/downloads/nodejs/8.10/$nodeSdkTarGz"
    dest new File(buildDir, nodeSdkTarGz)
    overwrite false
}

task extractNodeSdk(type: Copy, dependsOn: fetchGreengrassNodeSdk) {
    from tarTree(fetchGreengrassNodeSdk.dest)
    into buildDirTemp
}

task downloadNodeSdk(type: Copy, dependsOn: extractNodeSdk) {
    from "$buildDirTemp/aws-greengrass-core-sdk-js-1.4.0/sdk/$nodeSdkZip"
    into "$buildDirFoundation"
}

def javaSdkTarGz = 'aws-greengrass-core-sdk-java-1.3.1.tar.gz'
def javaSdkJar = 'GreengrassJavaSDK-1.3.1.jar'

task fetchGreengrassJavaSdk(type: Download) {
    src "https://d1onfpft10uf5o.cloudfront.net/greengrass-sdk/downloads/java/8/$javaSdkTarGz"
    dest new File(buildDir, javaSdkTarGz)
    overwrite false
}

task extractJavaSdk(type: Copy, dependsOn: fetchGreengrassJavaSdk) {
    from tarTree(fetchGreengrassJavaSdk.dest)
    into buildDirTemp
}

task downloadJavaSdk(type: Copy, dependsOn: extractJavaSdk) {
    from "$buildDirTemp/aws-greengrass-core-sdk-java/sdk/$javaSdkJar"
    into "$buildDirFoundation"
    rename { String filename -> return filename + ".zip" }
}

task downloadAll {
    dependsOn downloadGreengrassBinaries
    dependsOn downloadNodeSdk
    dependsOn downloadJavaSdk
}

// This task will be enabled when Greengrass Core and the SDKs are available directly via S3
assemble.dependsOn downloadAll

lombok {
    // Processes Lombok annotations during build
    version = "$lombokVersion"
    sha256 = ''
}

group = 'com.awslabs.aws.greengrass.provisioner.AwsGreengrassProvisioner'
version = '1.0-SNAPSHOT'

description = """"""

shadowJar {
    // Create a shadow JAR with all of the necessary dependencies
    archiveName = 'AwsGreengrassProvisioner.jar'

    // Include the "dist" files which are the SDKs and Greengrass binaries
    into('/dist') {
        from fileTree("$buildDirDist")
    }

    // Include the foundation for the Java functions
    into('/foundation') {
        from fileTree("$buildDirFoundation")
    }
}

sourceCompatibility = 1.8
targetCompatibility = 1.8

tasks.withType(JavaCompile) {
    options.encoding = 'UTF-8'
}

repositories {
    mavenCentral()

    maven {
        url "https://repo.gradle.org/gradle/libs-releases-local/"
    }
}

sourceSets {
    integrationTest {
        java {
            compileClasspath += sourceSets.main.output + configurations.testCompile
            runtimeClasspath += output + compileClasspath + configurations.testRuntime

            srcDir file('src/integration-test/java')
        }
    }
}

task integrationTest(type: Test) {
    description = 'Runs the integration tests.'
    group = 'verification'
    testClassesDirs = sourceSets.integrationTest.output.classesDirs
    classpath = sourceSets.integrationTest.runtimeClasspath
    outputs.upToDateWhen { false }
    mustRunAfter test
}

dependencies {
    compile "com.google.inject:guice:$guiceVersion"

    compile "com.typesafe:config:$configVersion"
    compile "org.zeroturnaround:zt-zip:$ztZipVersion"
    compile "com.beust:jcommander:$jcommanderVersion"
    compile "org.slf4j:slf4j-log4j12:$slf4jVersion"
    compile "com.jcabi:jcabi-log:$jcabiVersion"
    compile "org.kamranzafar:jtar:$jtarVersion"
    compile "com.google.code.gson:gson:$gsonVersion"
    compile "com.spotify:docker-client:$dockerClientVersion"

    // For Docker-style names
    compile 'com.oblac:nomen-est-omen:1.2.3'

    // For scp/ssh support
    compile 'com.jcraft:jsch:0.1.55'

    compile "software.amazon.awssdk:greengrass:$awsSdk2Version"
    compile "software.amazon.awssdk:iam:$awsSdk2Version"
    compile "software.amazon.awssdk:iot:$awsSdk2Version"
    compile "software.amazon.awssdk:lambda:$awsSdk2Version"
    compile "software.amazon.awssdk:cloudformation:$awsSdk2Version"
    compile "software.amazon.awssdk:sts:$awsSdk2Version"
    compile "software.amazon.awssdk:ecr:$awsSdk2Version"
    compile "software.amazon.awssdk:ec2:$awsSdk2Version"
    compile "org.apache.maven.shared:maven-invoker:$mavenInvokerVersion"
    compile "org.apache.commons:commons-lang3:$commonsLangVersion"
    compile "org.gradle:gradle-tooling-api:$gradleDependencyVersion"
    compile "io.vavr:vavr:$vavrVersion"
    // Dependency added to fix - https://github.com/aws/aws-sdk-java-v2/issues/652
    compile "org.apache.httpcomponents:httpclient:$httpClientVersion"

    // https://mvnrepository.com/artifact/net.jodah/failsafe
    compile "net.jodah:failsafe:$jodahFailsafeVersion"

    compile "org.testcontainers:testcontainers:$testContainersVersion"

    testCompile "junit:junit:$junitVersion"
    testCompile "com.github.stefanbirkner:system-rules:$systemRulesVersion"
    testCompile "org.hamcrest:hamcrest-core:$hamcrestVersion"
    testCompile "org.mockito:mockito-core:$mockitoVersion"
}

// From: https://gist.github.com/matthiasbalke/3c9ecccbea1d460ee4c3fbc5843ede4a
task resolveDependencies {
    doLast {
        project.rootProject.allprojects.each { subProject ->
            subProject.buildscript.configurations.each { configuration ->
                // Check to prevent "Resolving configuration 'integrationTestImplementation' directly is not allowed" error
                if (!configuration.getName().contains("integrationTest")) {
                    resolveConfiguration(configuration)
                }
            }
            subProject.configurations.each { configuration ->
                // Check to prevent "Resolving configuration 'integrationTestImplementation' directly is not allowed" error
                if (!configuration.getName().contains("integrationTest")) {
                    resolveConfiguration(configuration)
                }
            }
        }
    }
}

void resolveConfiguration(configuration) {
    if (isResolveableConfiguration(configuration)) {
        configuration.resolve()
    }
}

boolean isResolveableConfiguration(configuration) {
    def nonResolveableConfigurations = ['apiElements', 'implementation',
                                        'runtimeElements', 'runtimeOnly',
                                        'testImplementation', 'testRuntimeOnly',
                                        'generatedImplementation', 'generatedRuntimeOnly']

    if (nonResolveableConfigurations.contains(configuration.getName())) {
        return false
    }
    return true
}
